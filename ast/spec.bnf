// === [ Lexical part] =========================================================

terminator
: ';' '\n' 
| ';' 
;

// --- [ Keywords ] ------------------------------------------------------------

keywordAs
: 'a' 's'
;

keywordBusinessObject
: 'b' 'u' 's' 'i' 'n' 'e' 's' 's' 'o' 'b' 'j' 'e' 'c' 't'
;

keywordNode
: 'n' 'o' 'd' 'e'
;

keywordRaises
: 'r' 'a' 'i' 's' 'e' 's'
;

keywordAction
: 'a' 'c' 't' 'i' 'o' 'n'
;

// --- [ Whitespaces (suppressed) ] --------------------------------------------
!whitespace 
: ' ' 
| '\t' 
| '\r' 
| '\n'
;

// --- [ Comments (suppressed) ] -----------------------------------------------

!comment
: _lineComment 
| _blockComment
;

_lineComment  
: '/' '/' { . } '\n' 
;

_blockComment : '/' '*' { . | '*' } '*' '/' ;

// --- [ Letters and digits ] --------------------------------------------------

_letter       	
: _unicodeLetter 
| '_' 
;

_unicodeLetter 	
: 'a' - 'z' 
| 'A' - 'Z' 
;

_decimalDigit  	
: '0' - '9' 
;

_octalDigit
: '0' - '7' 
;

_hexDigit      	
: _decimalDigit 
| 'a' - 'f' 
| 'A' - 'F' 
;

// --- [ Identifiers ] ---------------------------------------------------------

identifier 	
: _letter { _letter | _decimalDigit } 
;

// --- [ Integer literals ] ----------------------------------------------------

intLit      
: _decimalLit 
| _octalLit 
| _hexLit 
;

_decimalLit 
: '1' - '9' { _decimalDigit } 
;

_octalLit   
: '0' { _octalDigit } 
;

_hexLit     
: '0' ( 'x' | 'X' ) _hexDigit { _hexDigit } 
;

// --- [ Floating-point literals ] ---------------------------------------------

floatLit  
: _decimals '.' [ _decimals ] [ _exponent ]
| _decimals _exponent
| '.' _decimals [ _exponent ]
;

_decimals 
: _decimalDigit { _decimalDigit } 
;

_exponent 
: ( 'e' | 'E' ) [ '+' | '-' ] _decimals 
;

// --- [ String literals ] -----------------------------------------------------

stringLit
:  _rawStrLit 
| _interpretedStrLit 
;

_rawStrLit
: '`' { . } '`' 
;

_interpretedStrLit 
: '"' { . | '\\' '"' | _escapeChar } '"' 
;

// --- [ Single-character escapes ] --------------------------------------------

_escapeChar 
: '\\' 'n' 
| '\\' 'r' 
| '\\' 't' 
;

// --- [ Predeclared constants ] -----------------------------------------------

boolLit
: 't' 'r' 'u' 'e' 
| 'f' 'a' 'l' 's' 'e' 
;

// --- [ Operators ] -----------------------------------------------------------
_relOp      : '=' '=' | '!' '=' | '<' | '<' '=' | '>' | '>' '=' ;
_addOp      : '+' | '-' | '|' | '^' ;
_mulOp      : '*' | '/' | '%' | '<' '<' | '>' '>' | '&' | '&' '^' ;

<< import "github.com/Soontao/pdi-util/ast" >> 

// === [ Syntax part] ==========================================================

Program
: StatementList << ast.NewProgram($0, nil) >>
| StatementList BusinessObjectDefination << ast.NewProgram($0, $1) >>
;

StatementList
: Statement << ast.NewCommonList(nil, $0) >>
| StatementList Statement << ast.NewCommonList($0, $1) >>
;

Statement
: ImportStmt terminator	<< ast.NewStatement($0) >> 
| VariableDeclarationStmt terminator << ast.NewStatement($0) >> 
| ForeachStmt << ast.NewStatement($0) >> 
| IfStmt << ast.NewStatement($0) >> 
| RaiseStmt terminator << ast.NewStatement($0) >> 
| ReturnStmt terminator << ast.NewStatement($0) >> 
| AssignmentStmt terminator << ast.NewStatement($0) >> 
| ExpressionStmt terminator << ast.NewStatement($0) >> 
;

ImportStmt
: "import" Namespace << ast.NewImportDeclaration($1, nil) >> 
| "import" Namespace keywordAs Identifier << ast.NewImportDeclaration($1, $3) >> 
;

ReturnStmt
: "return"
| "return" Expression
;

VariableDeclarationStmt
: "var" Identifier << ast.NewVariableDeclarationStmt($1), nil >>
| VariableDeclarationStmt "=" Expression  << ast.GeneralAddProperty($0, "Value", $2), nil >>
| VariableDeclarationStmt ":" DataType << ast.GeneralAddProperty($0, "DataType", $2), nil >>
;

AssignmentStmt
: Expression "=" Expression
;

ForeachStmt
: "foreach" "(" "var" Identifier "in" Expression ")" StatementsBlock << ast.NewForeachStmt($3, $5, $7), nil >>
;

IfStmt
: "if" "(" Expression ")" StatementsBlock << ast.NewIfStmt($2, $4), nil >>
| IfStmt "else" StatementsBlock << ast.GeneralAddProperty($0, "Else", $2), nil >>
| IfStmt "else" IfStmt << ast.GeneralAddProperty($0, "ElseOf", $2), nil >>
;

RaiseStmt
: "raise" Expression
;

ExpressionStmt
: Expression
;

StatementsBlock
: "{" StatementList "}" << $1, nil >>
;

BusinessObjectDefination
: keywordBusinessObject DataType "{" BOItemList "}" << ast.NewBODefination(nil, $1, nil, $3 ) >>
| keywordBusinessObject DataType RaiseExpr "{" BOItemList "}" << ast.NewBODefination(nil, $1, $2, $5 ) >>
| AnnotationList keywordBusinessObject DataType "{" BOItemList "}" << ast.NewBODefination($0, $2, nil, $4 ) >>
| AnnotationList keywordBusinessObject DataType RaiseExpr "{" BOItemList "}"  << ast.NewBODefination($0, $2, $3, $5 ) >>
;

BOItemList
: AnnotatedBOItem  << ast.NewCommonList(nil, $0) >>
| BOItemList AnnotatedBOItem << ast.NewCommonList($0, $1) >>
;

// wrap
AnnotatedBOItem
: AnnotationList BOItem << ast.NewAnnotatedBOItem($0, $1) >>
| BOItem << ast.NewAnnotatedBOItem(nil, $0) >>
;

BusinessObjectNode
: keywordNode Identifier "{" BOItemList "}" << ast.NewBusinessObjectNode($1, $3) >>
| keywordNode Identifier Multiplicity "{" BOItemList "}" << ast.NewBusinessObjectNode($1, $4, $2) >>
| keywordNode Identifier RaiseExpr "{" BOItemList "}" << ast.NewBusinessObjectNode($1, $4, nil, $2) >>
| keywordNode Identifier Multiplicity RaiseExpr "{" BOItemList "}" << ast.NewBusinessObjectNode($1, $5, $2, $3) >>
;

BOItem
: ElementItem << $0, nil >>
| AssociationItem  << $0, nil >>
| MessageItem << $0, nil >>
| ActionItem << $0, nil >>
| BusinessObjectNode << $0, nil >>
;

MessageItem
: "message" Identifier "text" Value ":" DataTypeList terminator << ast.NewMessageItem($1, $3, $5), nil >>
;

AssociationItem
: "association" MultiplicityIdentifier << ast.NewAssociationItem($1, nil, nil, nil), nil >>
| AssociationItem "to" DataType << ast.GeneralAddProperty($0, "Target", $2), nil >>
| AssociationItem "valuation" "(" Expression ")" << ast.GeneralAddProperty($0, "Valuation", $3), nil >>
| AssociationItem "using" Identifier << ast.GeneralAddProperty($0, "Using", $2), nil >>
| AssociationItem terminator << $0, nil >>
;

ElementItem
: "element" Identifier ":" DataType terminator << ast.NewElementItem($1, $3, nil) >>
| "element" Identifier ":" DataType "=" Value terminator << ast.NewElementItem($1, $3, $5) >>
;

ActionItem
: keywordAction Identifier terminator << ast.NewActionItem($1, nil), nil >>
| keywordAction Identifier RaiseExpr terminator << ast.NewActionItem($1, $2), nil >>
;

ExpressionList
: Expression << ast.NewCommonList(nil, $0) >>
| ExpressionList "," Expression << ast.NewCommonList($0, $2) >>
;

Expression
: Expression "||" Term1 << ast.NewExpression("||", $0, $2), nil >>
| Term1
;

Term1
: Term1 "&&" Term2 << ast.NewExpression("&&", $0, $2), nil >>
| Term2 
;

Term2
: Term2 RelOp Term3 << ast.NewExpression($1, $0, $2), nil >>
| Term3
;

Term3
: Term3 "+" Term4 << ast.NewExpression("+", $0, $2), nil >>
| Term4
;

Term4
: Term4 "-" Term5 << ast.NewExpression("-", $0, $2), nil >>
| Term5
;

Term5
: Term5 "*" Term6 << ast.NewExpression("*", $0, $2), nil >>
| Term6
;

Term6
: Term6 "/" Term7 << ast.NewExpression("/", $0, $2), nil >>
| Term7
;

Term7
: Term7 "%" Term8 << ast.NewExpression("%", $0, $2), nil >>
| Term8
;

Term8
: "(" Expression ")"  << $1, nil >>
| UnaryExpr
;

UnaryExpr
: PrimaryExpr
| UnaryOp UnaryExpr
;

PrimaryExpr
: Identifier << $0, nil >>
| Value << $0, nil >>
| Selector << $0, nil >>
| PrimaryExpr Arguments << ast.NewFuncCallExpr($0, $1), nil >>
;

UnaryOp
: "+"
| "-"
| "!"
| "*"
| "&"
;

RelOp
: "==" << ast.ConvertToString($0), nil >>
| "!=" << ast.ConvertToString($0), nil >>
| "<=" << ast.ConvertToString($0), nil >>
| "<"  << ast.ConvertToString($0), nil >>
| ">=" << ast.ConvertToString($0), nil >>
| ">"  << ast.ConvertToString($0), nil >>
;

Arguments
: "(" ")"
| "(" ExpressionList ")"
;

DataTypeList
: DataType << ast.NewCommonList(nil, $0) >>
| DataTypeList "," DataType << ast.NewCommonList($0, $2) >>
;

DataType
: Namespace ":" Identifier << ast.NewDataType($0, $2) >>
| Identifier << ast.NewDataType($0, nil) >>
;

KeyValueList
: KeyValue  << ast.NewKeyValueList($0, nil) >>
| KeyValueList "," KeyValue << ast.NewKeyValueList($2, $0) >>
;

KeyValue
: Identifier "=" Value << ast.NewKeyValuePair($0, $2) >>
;

NamespaceList
: Namespace
| Namespace "," NamespaceList
;

AnnotationList
: Annotation << ast.NewCommonList(nil, $0) >>
| AnnotationList Annotation << ast.NewCommonList($0, $1) >>
;

Namespace
: Identifier << ast.NewNamespace($0, nil) >>
| Identifier "." Namespace << ast.NewNamespace($0, $2) >>
;

Selector
: Identifier << ast.NewSelector($0, nil) >>
| Identifier "." Selector << ast.NewSelector($0, $2) >>
;

Annotation
: "[" Identifier "]"  << ast.NewAnnotation($1, nil, nil) >>
| "[" Identifier "(" Value ")" "]" << ast.NewAnnotation($1, nil, $3) >>
| "[" Identifier "(" Namespace ")" "]" << ast.NewAnnotation($1, $3, nil) >>
;

RepeatTerminator
: terminator RepeatTerminator 
| empty	
;

RaiseExpr
: keywordRaises IdentifierList << ast.NewRaiseExpr($1) >>
;

IdentifierList
: Identifier  << ast.NewCommonList(nil, $0) >>
| IdentifierList "," Identifier << ast.NewCommonList($0, $2) >>
;

MultiplicityIdentifier
: Identifier << $0, nil >>
| Identifier Multiplicity << ast.GeneralAddProperty($0, "Multiplicity", $1), nil >>
;

Identifier
: identifier << ast.NewIdentifier($0) >> 
;

Multiplicity
: "[" Value "," Value "]" << ast.NewMultiplicity($1, $3) >>
| "[" Value "," "n" "]" << ast.NewMultiplicity($1, "n") >>
;

Value
: floatLit << ast.NewNumberValue($0) >>
| intLit << ast.NewNumberValue($0) >>
| stringLit << ast.NewStringValue($0) >>
| boolLit << ast.NewBoolValue($0) >>
| ComplexValue << $0, nil >>
;

ComplexValue
: "{" KeyValueList "}" << ast.NewComplexValue($1) >>
;
